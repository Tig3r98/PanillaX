java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    implementation project(':panillax-api')
    implementation project(':panillax-paper-v1_21_4')
    implementation project(':panillax-paper-v1_21_5')
    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT' //use 1.21.4 Paper API
    implementation 'org.bstats:bstats-bukkit:3.1.0'
}

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.gradleup.shadow:shadow-gradle-plugin:9.3.0'
    }
}

processResources {
    filesMatching('**plugin.yml') {
        expand(
                'version': project.parent.version
        )
    }
}

apply plugin: 'com.gradleup.shadow'

shadowJar {
    relocate("de.tr7zw.changeme.nbtapi", "com.ruinscraft.panilla.lib.nbtapi")
    relocate("org.bstats", "com.ruinscraft.panilla.lib.bstats")

    archiveFileName.set("PanillaX-${project.version}.jar")
    destinationDirectory.set(new File("../target"))
}

build.dependsOn(shadowJar)